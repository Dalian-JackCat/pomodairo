<?xml version="1.0" encoding="utf-8"?>
<s:BorderContainer
	xmlns:fx="http://ns.adobe.com/mxml/2009"
	xmlns:s="library://ns.adobe.com/flex/spark"
	xmlns:mx="library://ns.adobe.com/flex/mx"
	x="10" y="5" width="298" height="154" backgroundColor="#313131"
	creationComplete="init()">

	<fx:Script>
        <![CDATA[
			import com.pomodairo.db.Storage;
			import com.pomodairo.events.ConfigurationUpdatedEvent;
			import com.pomodairo.PomodoroEventDispatcher;
			import flash.utils.Dictionary;
        	
        	public static var DATABASE_LOCATION:String = "db.location";
        	
        	public var dataFileLocation:String = "";
        	
        	private var dataFileFolder:File = new File();
        	
        	public static function getDatabaseLocation():String {
        		return getDBLocation();
        	}
        	
        	private function init():void
        	{
        		populate();
        		applyGuiValues();	
        	}
        	
        	public function populate():void
        	{
        		dataFileLocation = getDatabaseLocation();
        		
        		var props:Dictionary = Storage.instance.config;
				        		
        	}
        	
        	private function applyGuiValues():void
            {

        		dataFileLocationInput.text = dataFileLocation;
            }
        	
            private function exit():void
            {
				this.parent.removeChild (this);
            }
            
            public function save():void
            {

            	dataFileLocation = dataFileLocationInput.text;
            	storeDBLocation(dataFileLocation);
            	exit();
            	notifyConfiguration();
            }
            
            public function notifyConfiguration():void
            {
            	// Notify the world 
            	PomodoroEventDispatcher.getInstance().dispatchEvent(new ConfigurationUpdatedEvent(ConfigurationUpdatedEvent.UPDATED, DATABASE_LOCATION, dataFileLocation));
            }
            
			private function selectFile():void {
				dataFileFolder.addEventListener(Event.SELECT, selectHandler);
				dataFileFolder.browseForDirectory("Select Directory");
			}
			 
			private function selectHandler(event:Event):void {
				dataFileLocationInput.text = dataFileFolder.nativePath;
				dataFileLocation = dataFileFolder.nativePath;
			}      
			
			
			// Store database location in local store
			private static function storeDBLocation(value:String):void {
				try {
				    var data:ByteArray = new ByteArray();
				    data.writeUTFBytes(value);
				    EncryptedLocalStore.setItem(DATABASE_LOCATION, data);
				}  catch (e:Error) {
					trace("Failed to store value in the EncryptedLocalStore: "+e);
				}
			}
			
			// read database location from local store
			private static function getDBLocation():String {
				try {
				    var bytes:ByteArray = EncryptedLocalStore.getItem(DATABASE_LOCATION);
				    if (bytes) {
				        return bytes.readUTFBytes(bytes.bytesAvailable);
				    }
				}  catch (e:Error) {
					trace("Failed to access the EncryptedLocalStore: "+e);
				}
				return null;
			}   

			            
        ]]>
    </fx:Script>

	<s:Label x="10" y="10" color="#A9A9A9" text="Database location:"/>
	<s:TextInput id="dataFileLocationInput" x="10" y="26" width="203">
		<s:toolTip>Folder that will hold the pomodairo database file. Leave empty for default. NOTE: If you change this you will lose your current data!</s:toolTip>
	</s:TextInput>
	<s:Button x="221" y="25" label="Browse" click="selectFile()"/>
	
	<s:Button x="221" y="90" label="Save" click="save()" width="63"/>
	<s:Button x="221" y="121" label="Cancel" click="exit()"/>
	
</s:BorderContainer>
